#!/usr/bin/env nu

use modules/database.nu *
use modules/general.nu *

# Your own novel list in simple script!
#
# Add, view, edit and remove your novels within local database.
# Database file is located at "~/.local/share/novelist/novels.db"
def main [] {
  help main
}

# list novels in database
def "main show" [category: string = "all"] {
  load-database

  let list = if $category == "all" {
    stor open | query db "SELECT * FROM novels"
  } else {
    stor open | query db "SELECT title, chapters FROM novels WHERE category LIKE ?" -p [$category]
  }

  print $list
}

# add novel to the database
def "main add" [title? chapters? category?] {
  load-database

  let title = if ($title | is-not-empty) {$title} else { input "Plese enter novel title: " }
  let chapters = if ($chapters | is-not-empty) {$chapters} else { input "Chapters: " }
  let category = if ($category | is-not-empty) {$category} else { input "Category: " }
  stor insert -t novels -d {title: $title, chapters: $chapters, category: $category}

  save-database
}

# list categories
def "main categories" [] {
  load-database

  stor open | query db "SELECT DISTINCT category FROM novels" | print $in
}

# edit title in database
def "main edit" [title_to_edit? new_chapters? new_category? --skip-prompt(-s)] {
  load-database

  let title = search-novel-title $title_to_edit

  let new_title = if ($skip_prompt) {null} else {input "New title(leave empty to skip): "}
  let new_chapters = if ($skip_prompt or ($new_chapters | is-not-empty)) { $new_chapters } else { input "Update chapters: " }
  let new_category = if ($skip_prompt or ($new_category | is-not-empty)) { $new_category } else { input "Update category: " }

  if ($new_chapters | is-not-empty) {
    stor open |
      query db "UPDATE novels SET chapters = ? WHERE title LIKE ?" -p [$new_chapters $title]
  }

  if ($new_category | is-not-empty) {
    stor open |
      query db "UPDATE novels SET category = ? WHERE title LIKE ?" -p [$new_category $title]
  }

  if ($new_title | is-not-empty) {
    stor open |
      query db "UPDATE novels SET title = ? WHERE title LIKE ?" -p [$new_title $title]
  }

  print "Updated record:"
  stor open |
    query db "SELECT * FROM novels WHERE title LIKE ? OR title LIKE ?" -p [$title $new_title] |
    print $in

  save-database
}

# remove title from database
def "main remove" [title?] {
  load-database

  let title = search-novel-title $title

  stor open |
    query db "DELETE FROM novels WHERE title LIKE ?" -p [$title]
  print $"Deleted \"($title)\""

  save-database
}
